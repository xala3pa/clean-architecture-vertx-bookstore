plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
    id 'org.unbroken-dome.test-sets' version '2.1.1'
}

testSets {
    integrationTest
}

ext {
    vertxVersion = '3.6.3'
}

group 'vertx.io.xala3pa'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8
mainClassName = 'io.vertx.core.Launcher'

def mainVerticleName  = 'com.xala3pa.books.RestVertxApplication'
def watchForChange = 'src/**/*'
def doOnChange = './gradlew classes'

repositories {
    mavenLocal()
    jcenter()
}

dependencies {
    implementation project(':adapter:vertx-controller')
    implementation project(':config')
    implementation "io.vertx:vertx-web:$vertxVersion"
    testImplementation "io.vertx:vertx-web-client:$vertxVersion"
    testImplementation group: 'io.vertx', name: 'vertx-unit', version: '3.0.0'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.assertj:assertj-core:3.11.1'
    integrationTestImplementation 'io.rest-assured:rest-assured:3.3.0'
    integrationTestImplementation group: 'pl.pragmatists', name: 'JUnitParams', version: '0.3.6'
    integrationTestImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.0.1'
}

shadowJar {
    classifier = 'fat'
    manifest {
        attributes 'Main-Verticle': mainVerticleName
    }
    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}

run {
    args = ['run', mainVerticleName, "--redeploy=$watchForChange", "--launcher-class=$mainClassName", "--on-redeploy=$doOnChange"]
}

task wrapper(type: Wrapper) {
    gradleVersion = '5.1'
}